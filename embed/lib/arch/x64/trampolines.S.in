.section ".text.trampolines"

.global __lfi_trampoline
__lfi_trampoline:
	pushq %r15
	pushq %r14
	pushq %r13
	pushq %r12
	pushq %rbx
	pushq %rbp
	movq lfi_myctx@gottpoff(%rip), %r11
	movq %fs:(%r11), %r11

	cmpq $0, %r11
	jne 1f
	# slow path
	pushq %rdi
	pushq %rdi # for stack alignment
	pushq %rsi
	pushq %rdx
	pushq %rcx
	pushq %r8
	pushq %r9
	pushq %r10
	pushq %r11
	mov %rdi, %r11
	callq {{.lib}}_setup
	popq %r11
	popq %r10
	popq %r9
	popq %r8
	popq %rcx
	popq %rdx
	popq %rsi
	popq %rdi
	popq %rdi
	movq lfi_myctx@gottpoff(%rip), %r11
	movq %fs:(%r11), %r11
1:

	movq %rsp, 0(%r11) # kstackp
	movq 16(%r11), %rsp # stack pointer
	movq 16+14*8(%r11), %r14 # base pointer
	mov __lfisym__lfi_retfn@GOTPCREL(%rip), %r11
	mov (%r11), %r11
	andq $0xfffffffffffffff0, %rsp
	pushq %r11
	xor %r11d, %r11d
	# TODO: make sure r10 is bundle-aligned and within the sandbox
.entry:
	jmpq *%r10
	int3

.global __lfi_trampolines
__lfi_trampolines:
{{- range $sym := .exported }}
.p2align 4
.global {{$sym}}
{{$sym}}:
	movq __lfisym_{{$sym}}@GOTPCREL(%rip), %r10
	movq (%r10), %r10
	jmp __lfi_trampoline
{{- end}}

{{- range $sym := .exposed}}
.p2align 4
.global {{$.lib}}_{{$sym}}
{{$.lib}}_{{$sym}}:
	movq __lfisym_{{$sym}}@GOTPCREL(%rip), %r10
	movq (%r10), %r10
	jmp __lfi_trampoline
{{- end}}

.section ".data.trampolines"

.global __lfi_trampotable
__lfi_trampotable:

{{- range $sym := .exported}}
__lfisym_{{$sym}}:
	.quad 0
{{- end}}

{{- range $sym := .exposed}}
.global __lfisym_{{$sym}}
__lfisym_{{$sym}}:
	.quad 0
{{- end}}

.global __lfi_trampotable_size
__lfi_trampotable_size:
	.quad {{.nexported}}+{{.nexposed}}

.global __lfisym__lfi_pause
.global __lfisym__lfi_thread_create

.section .note.GNU-stack,"",@progbits
